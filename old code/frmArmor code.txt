

Private Sub cboArmorType_Click()
' Choose Armor Type.
    UpdateArmor
End Sub

Private Sub chkBalance_Click()
' Check/uncheck the Balance Armor checkbox.
    If chkBalance.Value Then vsbLWArmor.Value = vsbRWArmor.Value
    UpdateArmor
End Sub

Public Sub cmdClose_Click()
' Click on the Close button. (called also from main form)
    frmArmor.Visible = False
    Armor.C = vsbCArmor.Value
    Armor.F = vsbFArmor.Value
    Armor.LW = vsbLWArmor.Value
    Armor.RW = vsbRWArmor.Value
    Armor.Total = lblArmorFactor.Caption
    Armor.Size = lblArmorSpace.Caption
    
    With frmMain
        .lblArmor = Armor.Total
        .lblCArmor = Armor.C
        .lblFArmor = Armor.F
        .lblLWArmor = Armor.LW
        .lblRWArmor = Armor.RW
    End With
    
    frmMain.lblArmorType = Armor.ArmType
    frmMain.lblArmorSpc = FormatNumber(Armor.Size, 2)
    
    Unload Me
    CalcEngine
End Sub

Private Sub cmdMaxArmor_Click()
' Click on the Maximize Armor button.
    vsbCArmor.Value = vsbCArmor.Min
    vsbFArmor.Value = vsbFArmor.Min
    
    If Craft.Wings Then
        vsbLWArmor.Value = vsbLWArmor.Min
        vsbRWArmor.Value = vsbRWArmor.Min
    End If
    
    UpdateArmor
End Sub

Private Sub cmdShields_Click()
' Click on the Modify Shields button.
    frmChgShd.Visible = True
End Sub
Private Sub Form_Load()
    Dim AType As Integer
        
    lblShields.Caption = Craft.Shields
    lblShieldSpc.Caption = FormatNumber(Craft.Shields / 20, 2)
    
    If cboArmorType.ListCount = 4 And (Craft.Techbase = "H") Then
        cboArmorType.AddItem ("Clear Plast")
    End If
            
    If RTrim(Armor.ArmType) = "Standard" Then AType = 0
    If RTrim(Armor.ArmType) = "Didrate" Then AType = 1
    If RTrim(Armor.ArmType) = "Trinnium" Then AType = 2
    If RTrim(Armor.ArmType) = "Tri-Di Composite" Then AType = 3
    If RTrim(Armor.ArmType) = "Clear Plast" And Craft.Techbase = "H" _
        Then AType = 4
            
    cboArmorType.ListIndex = AType
    
    SetMaxArmor
    
    With Armor
        If .C > vsbCArmor.Min Then
            vsbCArmor.Value = vsbCArmor.Min
        Else
            vsbCArmor.Value = .C
        End If
        
        If .F > vsbFArmor.Min Then
            vsbFArmor.Value = vsbFArmor.Min
        Else
            vsbFArmor.Value = .F
        End If
        
        If .LW > vsbLWArmor.Min Then
            vsbLWArmor.Value = vsbLWArmor.Min
        Else
            vsbLWArmor.Value = .LW
        End If
        
        If .RW > vsbRWArmor.Min Then
            vsbRWArmor.Value = vsbRWArmor.Min
        Else
            vsbRWArmor.Value = .RW
        End If
    End With
    
    ' Hide the wings if fighter has no wings.
    If Craft.Wings = 0 Then
        lblLeftWing.Enabled = False
        lblLWArmor.Enabled = False
        vsbLWArmor.Enabled = False
        lblRightWing.Enabled = False
        lblRWArmor.Enabled = False
        vsbRWArmor.Enabled = False
    End If
    
End Sub

Private Sub Form_QueryUnload(Cancel As Integer, UnloadMode As Integer)
    If UnloadMode = 0 Then
        cmdClose_Click
    End If
End Sub

Private Sub vsbCArmor_Change()
' Change the Cockpit armor.
    lblCArmor.Caption = vsbCArmor.Value
    UpdateArmor
End Sub

Private Sub vsbFArmor_Change()
' Change the Fuselage armor.
    lblFArmor.Caption = vsbFArmor.Value
    UpdateArmor
End Sub

Private Sub vsbLWArmor_Change()
' Change the Left Wing armor.
    If chkBalance.Value Then vsbRWArmor.Value = vsbLWArmor.Value
    lblLWArmor.Caption = vsbLWArmor.Value
    UpdateArmor
End Sub

Private Sub vsbRWArmor_Change()
' Change the Right Wing armor.
    If chkBalance.Value Then vsbLWArmor.Value = vsbRWArmor.Value
    lblRWArmor.Caption = vsbRWArmor.Value
    UpdateArmor
End Sub

Public Sub UpdateArmor()
' Update armor totals on both the Armor form and Main form.
    Dim Total As Integer
    Dim Used As Integer
    Dim left As Integer
    Dim ArmorType As Integer
    Dim C As Integer
    Dim F As Integer
    Dim LW As Integer
    Dim RW As Integer
    
    C = vsbCArmor.Value
    F = vsbFArmor.Value
    LW = vsbLWArmor.Value
    RW = vsbRWArmor.Value
    
    Used = C + F + LW + RW
    Total = Used
    Do While Total Mod 5 <> 0
        Total = Total + 1
    Loop
    
    left = Total - Used
    
    lblArmorFactor.Caption = Total
    lblArmorLeft.Caption = left
    If left <> 0 Then
        lblArmorLeft.ForeColor = vbRed
    Else
        lblArmorLeft.ForeColor = vbBlack
    End If
    
    ArmorType = cboArmorType.ListIndex
    
    Select Case ArmorType
        Case 0
            Armor.ArmType = "Standard"
            lblArmorSpace.Caption = FormatNumber(Total * 0.6, 2)
        Case 1
            Armor.ArmType = "Didrate"
            lblArmorSpace.Caption = FormatNumber(Total * 0.4, 2)
        Case 2
            Armor.ArmType = "Trinnium"
            lblArmorSpace.Caption = FormatNumber(Total * 0.4, 2)
        Case 3
            Armor.ArmType = "Tri-Di Composite"
            lblArmorSpace.Caption = FormatNumber(Total * 0.2, 2)
        Case 4
            Armor.ArmType = "Clear Plast"
            lblArmorSpace.Caption = FormatNumber(Total * 0.1, 2)
    End Select
    
    Armor.Size = lblArmorSpace.Caption
    
    ' Update total space information.
    lblSpaceLeft.Caption = FormatNumber(SpaceLeft, 2)
    If lblSpaceLeft.Caption >= 0 Then
        lblSpaceLeft.ForeColor = vbBlack
    Else
        lblSpaceLeft.ForeColor = vbRed
    End If
      
End Sub

Private Sub SetMaxArmor()
    Dim Mult As Integer
    If Craft.Techbase = "N" Or Craft.Techbase = "I" Then
        Mult = 2
    Else
        Mult = 4
    End If
    
    vsbCArmor.Min = 5
    If RTrim(Criticals(1, 5).WeapName) = "Co-pilot" Or RTrim(Criticals(1, 6).WeapName) = "Co-pilot" Then _
    vsbCArmor.Min = 6
        
    vsbFArmor.Min = internal.F * Mult
    If Craft.Wings = 3 Then vsbFArmor.Min = vsbFArmor.Min * 1.5
    
    vsbLWArmor.Min = internal.LW * Mult
    vsbRWArmor.Min = internal.RW * Mult
End Sub
